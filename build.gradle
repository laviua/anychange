buildscript {
    ext.kotlin_version = '1.3.31'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.novoda:bintray-release:0.3.4"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'ua.com.lavi'

ext {
    ARTIFACT_ID = 'anychange'
    VERSION_NAME = project.version
    VERSION_CODE = project.version

    DESCRIPTION = 'AnyChange - Exchange any currency'

    SITE_URL = 'https://github.com/laviua/anychange'
    GIT_URL = 'https://github.com/laviua/anychange'
    GROUP_NAME = 'ua.com.lavi'

    LICENSE = 'APACHE-2.0'

    DEVELOPER_ID = 'Oleksandr Loushkin'
    DEVELOPER_NAME = 'Oleksandr Loushkin'
    DEVELOPER_EMAIL = 'developer@lavi.com.ua'

    IS_UPLOADING = project.getGradle().startParameter.taskNames.any { it.contains('bintrayUpload') }
}

repositories {
    maven {
        url 'https://dl.bintray.com/laviua/maven/'
    }
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    ktlint 'com.github.shyiko:ktlint:0.9.1'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

if (IS_UPLOADING) {

    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.getAllTasks().find {
            it.path == ":generatePomFileForMavenPublication"
        }.doLast {
            file("build/publications/maven/pom-default.xml").delete()
            println 'Overriding pom-file to make sure we can sync to maven central!'
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    name "$project.name"
                    artifactId ARTIFACT_ID
                    packaging 'jar'
                    description DESCRIPTION
                    url SITE_URL
                    version VERSION_NAME

                    scm {
                        url GIT_URL
                        connection GIT_URL
                        developerConnection GIT_URL
                    }

                    licenses {
                        license {
                            name LICENSE
                        }
                    }

                    developers {
                        developer {
                            id DEVELOPER_ID
                            name DEVELOPER_NAME
                            email DEVELOPER_EMAIL
                        }
                    }
                }
            }.writeTo("build/publications/maven/pom-default.xml")
        }
    }
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

//check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

publish {
    groupId = 'ua.com.lavi'
    artifactId = 'anychange'
    publishVersion = project.version
    desc = 'AnyChange - Exchange any currency'
    licences = ['APACHE-2.0']
    website = 'https://github.com/laviua/anychange'
}